C     ------------------------------------------------------------------
      SUBROUTINE TRASIM(IA,JA,IAT,JAT,M,N)
C     ------------------------------------------------------------------
C  THIS SUBROUTINE PERFORMS THE SYMBOLIC TRASPOSITION OF THE STRUCTURE
C  IA,JA OF A SPARSE MATRIX WITH N ROWS AND M COLUMNS.
C
C  DATA.
C  IA,JA  STRUCTURE OF THE SPARSE MATRIX,IN UNORDERED REPRESENTATION.
C        (IF IA(1) IS NOT 1, IS THE NUMBER OF ELEMENTS
C         IN EACH ROW (CONSTANT) THE REST OF IA IS NOT USED)
C  M  QUANTITY OF COLUMNS OF THE MATRIX.
C  N  QUANTITY OF ROWS OF THE MATRIX.
C  EXAMPLE=CALL TRASIM(IE,JE,IET,JET,N,NEL)
C
C  RESULTS.
C  IAT,JAT  STRUCTURE OF THE TRANSPOSE MATRIX,IN ORDERED REPRESENTATION.
C
C  SUBPROGRAMS=NONE.
C  STOP=0.
C
      LOGICAL SW
      DIMENSION IA(1),JA(1),IAT(2),JAT(1)
C
C1.M AND N MUST BE POSITIVE.
      SW = .FALSE.
      IF (IA(1).EQ.1) GO TO 80
      NEF= IA(1)
      SW = .TRUE.
80    IF(M.LT.1.OR.N.LT.1)GOTO 360
      MH=M+1
      NH=N+1
      DO 20 I=2,MH
20    IAT(I)=0
      IAB=N*NEF
      IF (.NOT.SW) IAB=IA(NH)-1
C2.IF IAB.LT.1 THEN THE MATRIX IS EMPTY.
      IF(IAB.LT.1)GOTO 360
      DO 30 I=1,IAB
      J=JA(I)+2
      IF(J.LE.MH)IAT(J)=IAT(J)+1
30    CONTINUE
C  THE QUANTITY OF NONZEROS IN COLUMN J-2,FOR J IN THE RANGE 3 TO
C  M+1,IS STORED IN IAT(J).
      IAT(1)=1
      IAT(2)=1
      IF(M.EQ.1)GOTO 10
      DO 40 I=3,MH
40    IAT(I)=IAT(I)+IAT(I-1)
C  THE QUANTITY OF NONZEROS +1 UP TO AND INCLUDING COLUMN I-2,FOR I-2
C  IN THE RANGE 1 TO M-1,IS STORED IN IAT(I). THUS,IAT(I) POINTS TO THE
C  POSITION IN JAT WHERE THE DESCRIPTION OF COLUMN I-1 BEGINS,FOR I-1
C  IN THE RANGE 1 TO M(SINCE IAT(2)=1).
C
10    DO 60 I=1,N
      IAB=I*NEF
      IAA=IAB-NEF+1
      IF (SW) GO TO 70
      IAA=IA(I)
      IAB=IA(I+1)-1
      IF(IAB.GE.IAA) GO TO 70
      IF(IAB.EQ.IAA-1) GO TO 60
C3.IF IAB.LT.IAA-1,THEN THE GIVEN STRUCTURE IS WRONG.
      GO TO 360
70    DO 50 JP=IAA,IAB
C  A NONZERO IS FOUND IN COLUMN J-1.
      J=JA(JP)+1
C  K POINTS TO THE POSITION IN JAT WHERE COLUMN J-1 BEGINS.
      K=IAT(J)
C  THE NONZERO JUST FOUND IS RECORDED AND IAT(J) IS INCREASED.
      JAT(K)=I
50    IAT(J)=K+1
60    CONTINUE
      RETURN
C
360   WRITE(6,365)M,N,I,IAA,IAB
      STOP ' '
365   FORMAT(13H TRASIM.ERROR,20I5)
      END
